plugins {
	id 'fabric-loom' version '1.9-SNAPSHOT'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Dependencies
	maven {
		url = "https://maven.terraformersmc.com"
	}
	maven {
		url = "https://jitpack.io"
	}

	// Transitive
	maven {
		url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
	}
	maven {
		url = "https://maven.blamejared.com"
	}
	maven {
		url = "https://maven.ladysnake.org/releases"
	}
	maven {
		url = "https://maven.shedaniel.me"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
}

dependencies {
	mappings loom.layered {
		mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
		// Override some mappings where yarn conflicts with dependencies
		// This ensures compatibility with mods using mojang mappings
		mappings(file("compat_fix_override.tiny"))
	}

	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

	// Dependencies
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")

	modImplementation(files("libs/biomemakeover-FABRIC-1.18.2-1.4.32.jar"))
	modImplementation(files("libs/chipped-fabric-1.18.2-2.0.1.jar"))
	modImplementation(files("libs/creeperoverhaul-1.18.2-1.3.1-fabric.jar"))
	modImplementation(files("libs/extractinator-fabric-1.18.2-2.0.1.jar"))
	modImplementation(files("libs/numismatic-overhaul-0.2.2+1.18.jar"))
	modImplementation(files("libs/tiered-1.1.1.jar"))
	modImplementation(files("libs/tokenablefurnaces-1.3.0.jar"))

	modImplementation("com.github.apace100:apoli:v2.3.3") {
		exclude group: "com.github.DaFuqs", module: "AdditionalEntityAttributes"
	}
	modCompileOnly("dev.emi:emi:0.7.3+1.18.2")

	// Transitive
	modImplementation(files("libs/AdditionalEntityAttributes-1.18.2-SNAPSHOT.jar"))
	modImplementation(files("libs/resourcefullib-fabric-1.18.2-0.0.4.jar"))
	modImplementation(files("libs/skylorlib-1.5.1-1.18.2.jar"))
	modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-base:4.1.4")
	modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:4.1.4")
	modImplementation("io.github.ladysnake:PlayerAbilityLib:1.5.0")
	modRuntimeOnly(files("libs/bclib-1.4.7.jar"))
	modRuntimeOnly(files("libs/ctm-fabric-1.0.0+1.18.jar"))
	modRuntimeOnly("dev.architectury:architectury-fabric:4.11.93")
	modRuntimeOnly("software.bernie.geckolib:geckolib-fabric-1.18:3.0.80")

	// Testing
	modRuntimeOnly(files("libs/EnchantmentDescriptions-Fabric-1.18.2-10.0.12.jar"))
	modRuntimeOnly(files("libs/mantori-1.1.1-1.18.2.jar"))
	modRuntimeOnly("net.darkhax.bookshelf:Bookshelf-Fabric-1.18.2:13.3.56")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}